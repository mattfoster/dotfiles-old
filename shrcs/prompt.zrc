# Super-duper themable prompt {{{
function my_prompt {
	# Colours accepted as arguments
	h_col=${1:-'green'}
	d_col=${2:-'blue'}
	n_tru=${3:-'blue'}
	n_fal=${4:-'yellow'}
	# looks like: ee1mpf@eepc-tsar16 ~ $
	ps1=(
		"%{$fg_bold[$h_col]%}%n@%m "
		"%{$fg_bold[$d_col]%}%1~ "
		"%{%(?.$fg_bold[$n_tru].$fg_bold[$n_fal])%}$"
		" %{$reset_color%}"
	)
	PS1="${(j::)ps1}"
}
# }}}

# Set xterm/screen titles. These should run as hooks {{{
function title {
	if [[ $TERM == "screen" ]]; then
		# Use these two for GNU Screen:
		print -nPR $'\033k'$1$'\033'\\\

		print -nPR $'\033]0;'$2$'\a'
	elif [[ $TERM == "xterm" || $TERM == "rxvt" ]]; then
		# Use this one instead for XTerms:
		print -nPR $'\033]0;'$*$'\a'
	fi
}

function precmd {
	title "%n@%m: $PWD"
}

function preexec {
	emulate -L zsh
  	local -a cmd; cmd=(${(z)1})
  	title $cmd[1]:t "$cmd[2,-1]"
}
# }}}
# vim:foldmethod=marker:filetype=zsh
